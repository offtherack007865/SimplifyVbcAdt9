-- SQL Server Instance:  smg-sql01
USE [Utilities];
IF EXISTS (SELECT * FROM sys.objects WHERE type = 'P' AND OBJECT_ID = OBJECT_ID('adt.di_FinalizeHumanaCensus'))
   DROP PROC [adt].[di_FinalizeHumanaCensus]
GO
CREATE PROCEDURE [adt].[di_FinalizeHumanaCensus]
(
  @inputFullFilename [nvarchar] (300)
)

/* -----------------------------------------------------------------------------------------------------------
   Procedure Name  :  di_FinalizeHumanaCensus
   Business Analyis:
   Project/Process :   
   Description     :  Finalize HumanaCensus table.
	  
   Author          :  Philip Morrison 
   Create Date     :  6/18/2025

   ***********************************************************************************************************
   **         Change History                                                                                **
   ***********************************************************************************************************

   Date       Version    Author             Description
   --------   --------   -----------        ------------
   6/18/2025  1.01.001   Philip Morrison    Created
   6/23/2025  1.01.002   Philip Morrison    Correct the version format from 1.00.001 to 1.01.002
                                            Eschew copying the Dummy1 field from [Staging].[adt].[HumanaCensus].
                                            The schema for the HumanaCensus table has changed to adt from dbo   
*/ -----------------------------------------------------------------------------------------------------------                                   

AS
BEGIN

-- Instance Declarations.
declare @IsOk [bit] 
declare @MyRecordCount [int] 

-- Template Declarations
DECLARE @Application            varchar(128) = 'SimplifyVbcAdt' 
DECLARE @Version                varchar(25)  = '1.01.002'

DECLARE @ProcessID              int          = 0
DECLARE @Process                varchar(128) = 'HumanaCensusAdt'

DECLARE @BatchOutID             int
DECLARE @BatchDescription       varchar(1000) = @@ServerName + '  - ' + @Version
DECLARE @BatchDetailDescription varchar(1000)
DECLARE @BatchMessage           varchar(MAX)
DECLARE @User                   varchar(128) = SUSER_NAME()

DECLARE @AnticipatedRecordCount int 
DECLARE @ActualRecordCount      int

SET NOCOUNT ON

BEGIN TRY

--  Initialize Batch
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  NULL, 'BatchStart', @BatchDescription, @ProcessID, @Process
----------------------------------------------------------------------------------------------------------------------------------------------------

    SET @BatchDetailDescription = '010/030:  Truncate HumanaCensusAdtMaster'
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailStart', @BatchDetailDescription
	
	  SELECT @AnticipatedRecordCount = COUNT(*)
	                                   FROM [SimplifyVbcAdt8].[dbo].[HumanaCensusAdtMaster];
	  
    -- Truncate HumanaCensusAdtMaster
    TRUNCATE TABLE [SimplifyVbcAdt8].[dbo].[HumanaCensusAdtMaster];
	
    SET @ActualRecordCount = @@ROWCOUNT
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailEnd', NULL, NULL, NULL, @AnticipatedRecordCount, @ActualRecordCount
----------------------------------------------------------------------------------------------------------------------------------------------------

    SET @BatchDetailDescription = '020/030:  Copy HumanaCensusAdt to HumanaCensusAdtMaster'
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailStart', @BatchDetailDescription
	
	  SELECT @AnticipatedRecordCount = COUNT(*)
	                                   FROM [Staging].[adt].[HumanaCensus];
	  
    -- Copy HumanaCensusAdt to HumanaCensusAdtMaster
    INSERT INTO [SimplifyVbcAdt8].[dbo].[HumanaCensusAdtMaster]
	(
      [PatientName]
      ,[PatientLastName]
      ,[PatienFirstName]
      ,[DateOfBirth]
      ,[PaneledProvider]
      ,[SdohDetails]
      ,[SrfDetails]
      ,[SrfFlagYOrN]
      ,[StayType]
      ,[AdmitDate]
      ,[DischargeDate]
      ,[AuthStatus]
      ,[Disposition]
      ,[PatEligible]
      ,[PatEligibleThrough]
      ,[ReadmitRisk]
      ,[Readmit]
      ,[Category]
      ,[DxDescription]
      ,[FacilityOrPractitioner]
      ,[NewYOrN]
      ,[FullFilename]
	)
    SELECT
	  REPLACE([PatientName],'^', ',')
      ,[PatientLastName]
      ,[PatienFirstName]
      ,[DateOfBirth]
      ,[PaneledProvider]
      ,REPLACE([SdohDetails],'^', ',')
      ,REPLACE([SrfDetails],'^', ',')
      ,[SrfFlagYOrN]
      ,[StayType]
      ,[AdmitDate]
      ,[DischargeDate]
      ,[AuthStatus]
      ,REPLACE([Disposition],'^', ',')
      ,[PatEligible]
      ,[PatEligibleThrough]
      ,[ReadmitRisk]
      ,[Readmit]
      ,[Category]
      ,REPLACE([DxDescription],'^', ',')
      ,REPLACE([FacilityOrPractitioner],'^', ',')
      ,[NewYOrN]
      ,@inputFullFilename
	FROM [Staging].[adt].[HumanaCensus]
    WHERE [DischargeDate] <> '-';
	
    SET @ActualRecordCount = @@ROWCOUNT
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailEnd', NULL, NULL, NULL, @AnticipatedRecordCount, @ActualRecordCount

----------------------------------------------------------------------------------------------------------------------------------------------------

    SET @BatchDetailDescription = '030/030:  Send Output that Copy worked.'
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailStart', @BatchDetailDescription
	
	  SELECT @AnticipatedRecordCount = 1;
	
    -- Did Copy work
    SELECT @MyRecordCount = COUNT(*)
                            FROM [SimplifyVbcAdt8].[dbo].[HumanaCensusAdtMaster];
    
    -- Send Output indication of whether Truncate worked.
    IF @MyRecordCount = 0 BEGIN
      SET @IsOk = 0;
    END
    ELSE BEGIN
      SET @IsOk = 1;
    END
    
    SELECT @IsOk as [IsOk];
    
    
    SET @ActualRecordCount = @@ROWCOUNT
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailEnd', NULL, NULL, NULL, @AnticipatedRecordCount, @ActualRecordCount
    
----------------------------------------------------------------------------------------------------------------------------------------------------
--  Close Batch
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT, @BatchOutID, 'BatchEnd'


END TRY


BEGIN CATCH
DECLARE @Err              int
     ,  @ErrorMessage     varchar(Max)
     ,  @ErrorLine        varchar(128)
     ,  @Workstation      varchar(128) = @Application
     ,  @Procedure        VARCHAR(500)

    IF ERROR_NUMBER() IS NULL 
      SET @Err =0;
    ELSE
      SET @Err = ERROR_NUMBER();

    SET @ErrorMessage = ERROR_MESSAGE()
    SET @ErrorLine    = 'SP Line Number: ' + CAST(ERROR_LINE() as varchar(10)) 
    
	SET @Workstation  = HOST_NAME()
	
    SET @Procedure    = @@SERVERNAME + '.' + DB_NAME() + '.' + OBJECT_SCHEMA_NAME(@@ProcID) + '.' + OBJECT_NAME(@@ProcID) + ' - ' + @ErrorLine + ' - ' + LEFT(@BatchDetailDescription, 7)
    EXEC Admin.Utilities.administration.di_ErrorLog  @Application ,@Process, @Version ,0, @ErrorMessage, @Procedure,  @User , @Workstation

    SET @BatchMessage = 'Process Failed:  ' +  @ErrorMessage
    EXEC Admin.Utilities.logs.di_batch @BatchOutID OUTPUT, @BatchOutID, 'BatchEnd', @BatchMessage
	
    RAISERROR(@ErrorMessage, 16,1)

END CATCH


END